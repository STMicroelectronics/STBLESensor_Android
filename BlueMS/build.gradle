/*
 * Copyright (c) 2017  STMicroelectronics â€“ All rights reserved
 * The STMicroelectronics corporate logo is a trademark of STMicroelectronics
 *
 * Redistribution and use in source and binary forms, with or without modification,
 * are permitted provided that the following conditions are met:
 *
 * - Redistributions of source code must retain the above copyright notice, this list of conditions
 *   and the following disclaimer.
 *
 * - Redistributions in binary form must reproduce the above copyright notice, this list of
 *   conditions and the following disclaimer in the documentation and/or other materials provided
 *   with the distribution.
 *
 * - Neither the name nor trademarks of STMicroelectronics International N.V. nor any other
 *   STMicroelectronics company nor the names of its contributors may be used to endorse or
 *   promote products derived from this software without specific prior written permission.
 *
 * - All of the icons, pictures, logos and other images that are provided with the source code
 *   in a directory whose title begins with st_images may only be used for internal purposes and
 *   shall not be redistributed to any third party or modified in any way.
 *
 * - Any redistributions in binary form shall not include the capability to display any of the
 *   icons, pictures, logos and other images that are provided with the source code in a directory
 *   whose title begins with st_images.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY
 * AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER
 * OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
 * SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
 * OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY
 * OF SUCH DAMAGE.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('MMddHHmm')
    return formattedDate
}

android {

    buildFeatures {
        viewBinding true
    }

    signingConfigs {
        config {
            storeFile file("../myreleasekey.jks")
            storePassword "password"
            keyAlias "MyReleaseKey"
            keyPassword "password"
        }
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion buildToolsVersion

    defaultConfig {

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 132
        versionName '4.18.1'
        applicationId "com.st.bluems"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        manifestPlaceholders = [
                'appAuthRedirectScheme': 'stassettracking'
        ]
        ndk {
            debugSymbolLevel  'FULL'
        }
    }
    buildTypes {
        release {
            //signingConfig signingConfigs.config
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //resValue 'string', 'application_id', android.defaultConfig.applicationId

            buildConfigField "String", "DTDL_DB_BASE_URL", "\"https://raw.githubusercontent.com/STMicroelectronics/appconfig/blesensor_4.18/\""
            buildConfigField "String", "DTDL_DB_BASE_URL_BETA", "\"https://raw.githubusercontent.com/SW-Platforms/appconfig/release/\""
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-dev" + getDate()
            //resValue 'string', 'application_id', android.defaultConfig.applicationId + applicationIdSuffix

            buildConfigField "String", "DTDL_DB_BASE_URL", "\"https://raw.githubusercontent.com/STMicroelectronics/appconfig/release/\""
            buildConfigField "String", "DTDL_DB_BASE_URL_BETA", "\"https://raw.githubusercontent.com/SW-Platforms/appconfig/release/\""
        }
    }

    sourceSets {
        all {
            res.srcDirs += ['st_images']
        }
    }

    productFlavors {

    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    //to use until all the string doesn't have a chinese translation

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    packagingOptions {
        resources {
            excludes += ['thirdpartynotice.txt', 'maven.properties', 'META-INF/DEPENDENCIES', 'META-INF/LICENSE.md', 'META-INF/NOTICE.md', 'META-INF/io.netty.versions.properties', 'META-INF/INDEX.LIST']
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    namespace 'com.st.BlueMS'

}

ext {
    leakcanaryVersion = '2.9.1'
}

repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    implementation project(':BlueSTSDK')
    implementation project(':BlueSTSDK_GUI')
    implementation project(':trilobytelib')
    implementation project(':BlueMSCloud')
    implementation project(':BlueMSCloudProprietary')
    implementation project(':BlueMSCloudSTAzure')
    implementation project(path: ':STWINBoard_GUI')
    implementation project(':UI')
    implementation project(':DataBases')


    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation group: 'com.google.android.material', name: 'material', version: "$rootProject.androidx_materialVersion"
    implementation group: 'androidx.legacy', name: 'legacy-support-v13', version: "${androidx_supportLegacy}"
    implementation group: 'androidx.gridlayout', name: 'gridlayout', version: "$rootProject.androidx_gridLayout"
    implementation group: 'androidx.appcompat', name: 'appcompat', version: "$rootProject.androidx_appCompatVersion"
    implementation group: 'androidx.fragment', name: 'fragment-ktx', version: "$rootProject.androidx_fragmentKtx"
    implementation group: 'androidx.cardview', name: 'cardview', version: "$rootProject.androidx_cardViewVersion"
    implementation group: 'androidx.constraintlayout', name: 'constraintlayout', version: "$rootProject.androidx_constraintLayoutVersion"
    implementation group: 'androidx.annotation', name:'annotation',version: "$rootProject.androidx_annotationVersion"
    implementation group: 'androidx.recyclerview', name:'recyclerview', version:"$rootProject.androidx_recycleViewVersion"
    implementation group: 'androidx.preference', name:'preference-ktx', version:"$rootProject.androidx_preferenceVersion"

    implementation group:'androidx.room', name:'room-runtime', version: "$rootProject.androidx_roomVersion"
    implementation group: 'androidx.lifecycle', name: 'lifecycle-livedata-ktx', version: "${androidx_lifecycleKtx}"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    annotationProcessor group:'androidx.room', name:'room-compiler', version: "$rootProject.androidx_roomVersion"

    // Lifecycle components
    implementation group: 'androidx.lifecycle', name: 'lifecycle-extensions', version: "${androidx_lifecycleExtVersion}"
    implementation group: 'androidx.lifecycle', name: 'lifecycle-viewmodel-ktx', version: "${androidx_lifecycleKtx}"
    implementation group: 'androidx.lifecycle', name: 'lifecycle-viewmodel-savedstate', version: "$rootProject.androidx_lifecycleSaveStateVersion"


    // ibm speech to text
    implementation(group: 'com.ibm.watson', name: 'speech-to-text', version: '8.1.0'){
        exclude group: 'com.google.code.findbugs', module: 'annotations'
    }

    //use the v 3.11 to avoid crash with ibm
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.3'

    // crashlytics
    // Import the BoM for the Firebase platform
    implementation platform('com.google.firebase:firebase-bom:31.2.1')

    // Add the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    //memory leak detector
    debugImplementation group: 'com.squareup.leakcanary', name: 'leakcanary-android', version: "${leakcanaryVersion}"

    testImplementation group: 'org.json', name: 'json', version: '20220320'
    //since the json lib is inside the android sdk
    testImplementation group: 'junit', name: 'junit', version: "$rootProject.junitVersion"
    testImplementation group: 'org.mockito', name: 'mockito-core', version: "$rootProject.mockitoVersion"

    androidTestImplementation group:'androidx.test', name:'runner', version:"$rootProject.androidx_runnerVersion"
    androidTestImplementation group:'androidx.test', name:'rules', version:"$rootProject.androidx_rulesVersion"
    androidTestImplementation group:'androidx.test.espresso', name: 'espresso-core', version:"$rootProject.androidx_espressoVersion"

    implementation group: 'com.squareup.retrofit2', name: 'retrofit', version: "$rootProject.retrofitVersion"
    implementation group: 'com.squareup.retrofit2', name: 'converter-gson', version: "$rootProject.retrofitVersion"
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    // JSON parsing
    implementation group: 'com.beust', name: 'klaxon', version: '5.6'
    implementation 'androidx.browser:browser:1.5.0'

    implementation "com.github.bumptech.glide:glide:$glideVersion"
}

